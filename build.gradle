plugins {
	id 'fabric-loom' version '0.9-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

version = project.build_version
group = "xyz.heroesunited"
archivesBaseName = "heroesunited"

loom {
	mixin {
		defaultRefmapName = "heroesunited-refmap.json"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 16
}

configurations {
	shade
}

shadowJar {
	configurations = [project.configurations.shade]
	from sourceSets.main.allSource
	exclude 'com/sun/jna/**'
	exclude 'software/bernie/example/**'
	exclude 'assets/geckolib3/**'
	relocate 'software.bernie.geckolib', 'software.bernie.HU.geckolib'

	classifier ''
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	classifier = 'deobf'
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}


artifacts {
	archives deobfJar
	archives sourcesJar
	archives shadowJar
}


java {
	withSourcesJar()
}

remapJar {
	dependsOn(shadowJar)
	input.set(shadowJar.archivePath)
}


repositories {
	maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
	maven { url 'https://jitpack.io' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation 'com.github.Vatuu:discord-rpc:1.6.2'
	modImplementation "software.bernie.geckolib:geckolib-fabric-1.17:$project.geckolib_version:dev"

	shade 'com.github.Vatuu:discord-rpc:1.6.2'
	shade 'com.github.Vatuu:discord-rpc-binaries:3.4.0'
	shade "software.bernie.geckolib:geckolib-fabric-1.17:$project.geckolib_version:dev"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact shadowJar
			artifact deobfJar
			artifact sourcesJar
			artifactId = "fabric-hu-core"
		}
	}
	repositories {
		maven {
			def repsyURL = System.getenv("repsyURL")
			url = repsyURL
			credentials {
				def envUsername = System.getenv("repsyUser")
				def envPassword = System.getenv("repsyPassword")
				username = envUsername
				password = envPassword
			}
		}
	}
}

